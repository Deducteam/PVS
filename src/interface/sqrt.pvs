sqrt: THEORY
%------------------------------------------------------------------------
%
%  Square root of non-negative real: AXIOMATIC VERSION
%
%  Supercedes older version: sqrt_ax
%
%------------------------------------------------------------------------

BEGIN
importing more_real_props


  IMPORTING sq    % sq(a): nonneg_real = a*a

  nnx, nny, nnz: VAR nonneg_real
  x,y,z,xx: VAR real

  sqrt_exists: AXIOM
     (EXISTS (f: [nnx: nonneg_real -> {nnz: nonneg_real | nnz * nnz = nnx}]):
                TRUE)

  sqrt(nnx): {nnz | nnz*nnz = nnx}

  sqrt_pos            : JUDGEMENT sqrt(px: posreal) HAS_TYPE posreal
  sqrt_nnr            : JUDGEMENT sqrt(px: nonneg_real) HAS_TYPE nonneg_real

% -------------------- Special Arguments --------------------

  sqrt_0              : LEMMA  sqrt(0) = 0
  sqrt_1              : LEMMA  sqrt(1) = 1
  sqrt_eq_0           : LEMMA  sqrt(nnx) = 0 IMPLIES nnx = 0

% -------------------- Basic Properties --------------------

  sqrt_lem            : LEMMA  sqrt(nny) = nnz IFF nnz * nnz = nny
                               
  sqrt_def            : LEMMA  sqrt(nnx) * sqrt(nnx) = nnx

  sqrt_square         : LEMMA  sqrt(nnx * nnx) = nnx

  sqrt_sq             : LEMMA  x >= 0 IMPLIES sqrt(sq(x)) = x

  sqrt_sq_abs         : LEMMA  sqrt(sq(x)) = abs(x)

  sq_sqrt             : LEMMA  x >= 0 IMPLIES sq(sqrt(x))=x

  sqrt_times          : LEMMA  sqrt(nny * nnz) = sqrt(nny) * sqrt(nnz)

  sqrt_div            : LEMMA  nnz /= 0 IMPLIES 
                                   sqrt(nny / nnz) = sqrt(nny) / sqrt(nnz)

% --------------------- Inequalities --------------------
                               
  sqrt_lt             : LEMMA  sqrt(nny) < sqrt(nnz) IFF nny < nnz 

  sqrt_le             : LEMMA  sqrt(nny) <= sqrt(nnz) IFF nny <= nnz 

  sqrt_gt             : LEMMA  sqrt(nny) > sqrt(nnz) IFF nny > nnz 

  sqrt_ge             : LEMMA  sqrt(nny) >= sqrt(nnz) IFF nny >= nnz 

  sqrt_eq             : LEMMA  sqrt(nny) = sqrt(nnz) IFF nny = nnz  

  sqrt_less           : LEMMA  nnx > 1 IMPLIES sqrt(nnx) < nnx

  sqrt_more           : LEMMA  nnx > 0 AND nnx < 1 IMPLIES sqrt(nnx) > nnx


END sqrt

